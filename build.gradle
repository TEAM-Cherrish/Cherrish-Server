plugins {
	id 'java'
	id 'org.springframework.boot' version '3.5.9'
	id 'io.spring.dependency-management' version '1.1.7'
	id 'jacoco'
	id 'checkstyle'
	id 'com.google.cloud.tools.jib' version '3.4.4'
}

group = 'com.sopt'
version = '0.0.1-SNAPSHOT'
description = 'WAS of Cherrish'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(21)
	}
}

repositories {
	mavenCentral()
}

dependencies {
	// Spring Boot Starters
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-validation'

	// Database
	runtimeOnly 'org.postgresql:postgresql'

	// Lombok
	compileOnly 'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok'

	// QueryDSL
	implementation 'com.querydsl:querydsl-jpa:5.1.0:jakarta'
	annotationProcessor "com.querydsl:querydsl-apt:5.1.0:jakarta"
	annotationProcessor "jakarta.annotation:jakarta.annotation-api"
	annotationProcessor "jakarta.persistence:jakarta.persistence-api"

	//swagger
	implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.8.14'
	implementation 'io.swagger.core.v3:swagger-annotations-jakarta:2.2.41'

	// Spring AI OpenAI
	implementation platform('org.springframework.ai:spring-ai-bom:1.1.2')
	implementation 'org.springframework.ai:spring-ai-starter-model-openai'

	// Test
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
	testRuntimeOnly 'com.h2database:h2'
}

tasks.named('test') {
	useJUnitPlatform()
}

tasks.named('bootBuildImage') {
	imageName = "${project.name}:${project.version}"

	environment = [
		'BP_JVM_VERSION': '21'
	]
}

// JaCoCo Configuration
jacoco {
	toolVersion = "0.8.12"
}

jacocoTestReport {
	dependsOn test
	reports {
		xml.required = true
		html.required = true
		csv.required = false
	}
}

jacocoTestCoverageVerification {
	violationRules {
		rule {
			limit {
				minimum = 0.0
			}
		}
	}
}

test {
	finalizedBy jacocoTestReport
}

// Checkstyle Configuration
checkstyle {
	toolVersion = "12.3.0"
	configFile = file("${rootDir}/config/checkstyle/checkstyle.xml")
	maxWarnings = 100
}

checkstyleMain {
	source = 'src/main/java'
}

checkstyleTest {
	source = 'src/test/java'
}

// QueryDSL 설정
def querydslDir = "$buildDir/generated/querydsl"

sourceSets {
	main.java.srcDirs += [querydslDir]
}

tasks.withType(JavaCompile).configureEach {
	options.generatedSourceOutputDirectory = file(querydslDir)
}

clean {
	delete file(querydslDir)
}

// Jib Configuration (Docker image build without Dockerfile)
jib {
	from {
		image = 'eclipse-temurin:21-jre'
	}
	to {
		image = "${System.getenv('ECR_REGISTRY') ?: 'cherrish-server'}"
		tags = ['latest', project.version.toString()]
	}
	container {
		jvmFlags = ['-Dspring.profiles.active=prod', '-Djava.security.egd=file:/dev/./urandom']
		ports = ['8080']
		environment = [
			'TZ': 'Asia/Seoul'
		]
		creationTime = 'USE_CURRENT_TIMESTAMP'
	}
}
